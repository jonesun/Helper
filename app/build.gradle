apply from: '/application.gradle'

android {
    compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
    buildToolsVersion project.BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "jone.helper"
        minSdkVersion Integer.parseInt(project.MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.TARGET_SDK_VERSION)
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName VERSION_NAME
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "UMENG"]

        multiDexEnabled = true
        multiDexKeepProguard file('multi-dex-keep.txt')
    }
    //打包签名
    signingConfigs {
        release {
            try {
                storeFile file('store.jks')
                storePassword KEYSTORE_PASSWORD
                keyAlias "jonesun"
                keyPassword KEY_PASSWORD
            }
            catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                def productFlavor = variant.productFlavors[0]
                String versionName = productFlavor.versionName
                if (versionName == null) {
                    versionName = defaultConfig.versionName
                }
                String versionCode = productFlavor.versionCode
                if (versionCode == null) {
                    versionCode = defaultConfig.versionCode
                }
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = outputFile.name.replace(".apk", "") + "_" + versionName + "_" + versionCode + "_${releaseTime()}.apk"
                        println("fileName: " + fileName)
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    sourceSets {
        main {
            //java.srcDirs = ['src/main/java']
            res.srcDirs = getSrcDirs('src/main/res', 'src/main/res-modules')
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

repositories {
    flatDir {
//        dirs rootDir.getPath() + File.separator + 'aars'
        dirs 'aars'
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile(name: 'Gallery3D-release1.0', ext: 'aar')
    //    compile(name: 'ZxingScan-release1.0', ext: 'aar')
    compile(name: 'yalantiscontextmenu-release1.0', ext: 'aar')
    compile project(':libraries:JoneLib')
    testCompile 'junit:junit:4.12'
    //    compile 'com.android.support:support-v4:23.0.1'
    compile project(':libraries:ZxingScan')
    compile 'com.google.zxing:core:3.0.1'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v13:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:palette-v7:23.1.1'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'cn.lightsky.infiniteindicator:library:1.0.3'
    compile project(':libraries:viewpagerindicator')
}

def getSrcDirs(defaultSrcDirs, customSrcDirsRoot) {
    ArrayList empty1 = [defaultSrcDirs]
    def tree = fileTree(dir: customSrcDirsRoot)
    tree.visit { element ->
        if (element.isDirectory() && element.relativePath.toString().indexOf("/") == -1) {
            empty1.add element.file.toString()
        }
    }
    return empty1;
}

def releaseTime() {
    return new Date().format("yyyy_MM_dd")
}



